model BookingSys

-- main control class

class BookingSystem
  attributes 
    date : Date
  
  operations      
    selectBooking(tno: Integer, hr: Integer, mn: Integer)
    begin      
        for r in self.current do
            if (r.table.tno = tno and r.getTime() = hr*60+mn) then
                insert(self,r) into Selected                
            end
        end
    end
    
    unSelectBooking()
    
    recordArrival(hr: Integer, mn: Integer)
    begin
        declare t: Time;
        t := new Time; 
        t.setTime(hr, mn);
        self.selected.setArrivalTime(t);
        WriteLine('Customer has just been seated ');
        self.selected.showDetails()
    end

    cancel()
    begin
      Write('The booking made by ');
      self.selected.getCustomerName();
      WriteLine(' was cancelled');
      destroy self.selected 
    end
    
    statemachines
        psm States
        states                        
            newBooking : initial
            notSelected
            selected
        transitions
            newBooking ->  notSelected  { create }
            notSelected -> selected { selectBooking() }
            selected -> selected { [self.selected.table.covers >= self.selected.covers] recordArrival() }
            selected -> selected { selectBooking() }
            selected -> notSelected { unSelectBooking() }
        end

end


class Booking
  attributes
    covers : Integer
    date : Date
    time : Time
  operations
    setArrivalTime(t: Time)
    begin
    end
    
    showDetails()
    begin
    end

    getCustomerName()
    begin
    end
    
    setTime(t : Time)
    begin
        self.time := t
    end
    
    -- A query operation to retun the time of the booking in minutes
    getTime() : Integer = time.getTime()
end

class WalkIn < Booking
end

class Reservation < Booking
  attributes
    arrivalTime : Time
  operations
    showDetails()
    begin
        Write('Selected reservation is by ');
        WriteLine(self.customer.name)
    end

    getCustomerName()
    begin
      Write(self.customer.name)
    end
    
    setArrivalTime(t: Time)
    begin
        self.arrivalTime := t
    end
    
    setCustomer(c : Customer) begin
        insert(c, self) into Makes        
    end

    statemachines
      psm States
      states  
        newReservation : initial
        waiting
        seated
      transitions
        newReservation -> waiting { create }
        waiting -> seated { setArrivalTime() }
      end

end

class Customer
  attributes
    name : String
    phoneNumber : String
end


class Table 
  attributes
    tno : Integer
    covers : Integer

end
   

   
class Date
  attributes
    day : Integer
    month : Integer
    year : Integer
end

class Time
  attributes
    hour : Integer
    min : Integer
  operations
    setTime(h: Integer, m: Integer)
    begin
        self.hour := h;
        self.min := m
    end 
    
    -- a query method to return the time since midnight in minutes
    getTime() : Integer = self.hour*60 + self.min      
end   

association IsAt between 
    Booking[*] role bookings
    Table[1] role table
end

association Makes between
    Customer[1]
    Reservation[*] role reservations
end

association Contains between
    BookingSystem[1] role bs
    Booking[*] role current
end

association Selected between
    BookingSystem[1] role bsys
    Booking[0..1] role selected
end


constraints

context BookingSystem::recordArrival(hr: Integer, mn: Integer)
    pre arrivalBeforeBooking: self.selected.getTime() <= mn + hr*60

    pre arrivalBeforeBooking2: self.selected.table.covers >= self.selected.covers




